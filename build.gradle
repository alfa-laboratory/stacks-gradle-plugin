buildscript {
  ext {
    nebula_project_plugin = '3.4.0'
    gradle_contacts_plugin = '3.0.1'
    gradle_dependency_lock_plugin = '5.0.0'
    nebula_dependency_recommender = '5.2.0'
    gradle_extra_configurations_plugin = '3.3.0'
    gradle_info_plugin = '3.7.1'
    nebula_publishing_plugin = '7.0.6'
    nebula_release_plugin = '6.3.2'
    jfrogExtractorVersion = '4.7.1'
    asciidoctorGradlePluginVersion = '1.5.7'
    asciidoctorjVersion = '1.5.6'
    asciidoctorjDiagramVersion = '1.5.8'

  }
  repositories {
    jcenter()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'com.gradle.publish:plugin-publish-plugin:0.9.10'
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.7.1'

    classpath "com.netflix.nebula:nebula-project-plugin:$nebula_project_plugin"
    classpath "com.netflix.nebula:gradle-contacts-plugin:$gradle_contacts_plugin"
    classpath "com.netflix.nebula:gradle-dependency-lock-plugin:$gradle_dependency_lock_plugin"
    classpath "com.netflix.nebula:nebula-dependency-recommender:$nebula_dependency_recommender"
    classpath "com.netflix.nebula:gradle-extra-configurations-plugin:$gradle_extra_configurations_plugin"
    classpath "com.netflix.nebula:gradle-info-plugin:$gradle_info_plugin"
    classpath "com.netflix.nebula:nebula-publishing-plugin:$nebula_publishing_plugin"
    classpath "com.netflix.nebula:nebula-release-plugin:$nebula_release_plugin"
  }
}

apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'nebula.dependency-recommender'
apply plugin: 'nebula.contacts'
apply plugin: 'nebula.info'
apply plugin: 'nebula.integtest'
apply plugin: 'nebula.javadoc-jar'
apply plugin: 'nebula.source-jar'
apply plugin: 'nebula.maven-publish'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'nebula.release'

ext {
  springBootVersion = '1.5.9.RELEASE'
}

repositories {
  jcenter()
  maven { url "https://plugins.gradle.org/m2/" }
}

group = 'ru.alfalab.gradle.platform.stack'

description 'Project with set of plugins for rapid development'

contacts {
  'tolk.kv@gmail.com' {
    moniker 'Tolkachev Kirill'
    github 'lavcraft'
  }
}

dependencies {
  compileOnly 'org.projectlombok:lombok:1.16.18'
  compile gradleApi()
  compile 'org.codehaus.groovy:groovy-all:2.4.12'

  compile "com.netflix.nebula:nebula-project-plugin:$nebula_project_plugin"
  compile "com.netflix.nebula:gradle-contacts-plugin:$gradle_contacts_plugin"
  compile "com.netflix.nebula:gradle-dependency-lock-plugin:$gradle_dependency_lock_plugin"
  compile "com.netflix.nebula:nebula-dependency-recommender:$nebula_dependency_recommender"
  compile "com.netflix.nebula:gradle-extra-configurations-plugin:$gradle_extra_configurations_plugin"
  compile "com.netflix.nebula:gradle-info-plugin:$gradle_info_plugin"
  compile "com.netflix.nebula:nebula-publishing-plugin:$nebula_publishing_plugin"
  compile "com.netflix.nebula:nebula-release-plugin:$nebula_release_plugin"

  compile "io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE"
  compile "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  compile "org.jfrog.buildinfo:build-info-extractor-gradle:${jfrogExtractorVersion}"
  compile "org.asciidoctor:asciidoctor-gradle-plugin:${asciidoctorGradlePluginVersion}"
  compile "org.asciidoctor:asciidoctorj:${asciidoctorjVersion}"
  compile "org.asciidoctor:asciidoctorj-diagram:${asciidoctorjDiagramVersion}"


  compile('com.github.jruby-gradle:jruby-gradle-plugin:1.5.0') {
    exclude group: 'org.eclipse.jetty'
    exclude group: 'de.saumya.mojo' //jruby is ... It broke wiremock
  }

  compile 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.7'

  testCompile 'com.netflix.nebula:nebula-test:6.1.2'
  testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile "com.github.tomakehurst:wiremock:2.14.0"
}

if (!project.version.toString().contains('-dev')) {
  logger.quiet 'Configure artifactory...'
  boolean isSnapshot = project.version.toString().contains('SNAPSHOT')
  String repo = isSnapshot ? 'snapshots' : 'releases'
  artifactory {
    publish {
      contextUrl = project.findProperty('artifactory_contextUrl')
      repository {
        repoKey = repo
        username = project.findProperty('artifactory_user')
        password = project.findProperty('artifactory_password')
      }
      defaults {
        publications('nebula', 'testReports')
        publishConfigs('archives')
      }
    }
  }

  tasks.artifactoryPublish.dependsOn tasks.findByPath('assemble')
}

tasks.withType(FindBugs) {
  ignoreFailures = true
  reports {
    xml.enabled = false
    html.enabled = true
  }
  classes = classes.filter {
    !it.name.endsWith('.json') || !it.name.endsWith('Spec.groovy')
  }
}

pluginBundle {
  plugins {
    stacksApp {
      id = 'stacks.app'
      displayName = 'Stacks Application Project Plugion'
      description = 'Configure Spring Boot Project'
      tags = ['springboot', 'plugin']
    }
  }
}